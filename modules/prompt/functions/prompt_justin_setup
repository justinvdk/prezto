#
# A simple theme that displays:
#   - Git branch.
#   - Git state.
#   - Last command exit state (->/X).
#
# Authors:
#   Nadav Shatz <nadavshatz@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/ijycV6n.png
#

# Load dependencies.
pmodload 'helper'

function prompt_justin_precmd {
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

}

function prompt_justin_setup {
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(percent subst)

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_justin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{blue}%b%f'
  zstyle ':prezto:module:git:info:dirty' format '%%B%F{red} ±%f%%b'
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%D)'

  # Define prompts.
  PROMPT='%F{15}%n${SSH_TTY:+"%f%F{7}@%f%F{14}%m%f "}%(?:%F{green}➜%f:%F{red}✖%f) ${git_info:+${(e)git_info[prompt]}} %B%c%b '
  RPROMPT='${editor_info[overwrite]}${VIM:+" %B%F{green}V%f%b"}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_justin_setup "$@"
